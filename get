#! /usr/bin/perl

use strict;

# use Barefoot::debug;							# comment out for production

use Getopt::Std;
use Cwd qw(getcwd);
use Barefoot::cvs;
use Barefoot::input qw(get_yn);

my $me = $0;
$me =~ s@^.*/@@;


# default values
my $verbose=0;
my $cvsroot=$::ENV{CVSROOT};
my $revdate="";
my $rev="";


#################################
# OPTIONS AND ENVIRONMENT VARS
#################################

my $opt = {};
getopts('vd:r:D:', $opt);
$verbose = 1 if defined $opt->{v};
$cvsroot = $opt->{d} if defined $opt->{d};
$revdate = $opt->{D} if defined $opt->{D};
$rev = $opt->{r} if defined $opt->{r};

#################################
# CHECK FOR ERRORS
#################################

if (!$cvsroot )
{
	print STDERR "$me: variable CVSROOT must be set\n";
	exit 2;
}

my $offsite = cvs::is_offsite($cvsroot);
my $module = $ARGV[0];
print "$me: Module $module\n";

if (!$module)
{
	print STDERR "usage: $me module\n";
	exit 2;
}

if (!-r $module)
{
	print STDERR "$me: $module does not exist or is not readable\n";
	exit 2;
}

my ($project, $subdir) = cvs::parse_module(getcwd());


if ( !$offsite )
{
	if (!-e "$cvsroot/$project/$subdir/$module,v")
	{
		print "$cvsroot/$project/$subdir/$module,v\n";
		print STDERR "$me: $module does not appear to be checked into CVS\n";
		exit 2;
	}
}


#################################
# MAIN
#################################

my $user = $::ENV{USER};
$user = $::ENV{USERNAME} if !$user;

my $user_is_locker = 0;
my @lockers = cvs::getLockers($cvsroot, $module, \$user_is_locker);
if (@lockers)
{
	# inform user that others are editing the file
	print "$me info: $module is currently being edited by: @lockers\n";
	if ( !$offsite )
	{
		if (!get_yn("Do you want to edit it anyway?"))
		{
			exit 1;
		}
	}
}

# check to see if the user is already editing the file
print "$me:  you are already editing the file.\n" if $user_is_locker;

if ( $revdate || $rev )
{ 
	# this is a precaution, as I f#cked myself with conflicts by 
	# checking out older version on top of edited new version
	die "$me: can not check out older version if editing." if $user_is_locker;

	# -f gets the latest version if -r or -D fail
	my $options = "-f";
	$options = $options . " -D $revdate" if $revdate;
	$options = $options . " -r $rev" if $rev;
	print "$me: Syncing $module using $options.\n";
	system("cvs -r -d $cvsroot update $options $module");
}
else
{
	# -A resets any stick tags
	print "$me: Syncing $module to latest version.\n";
	system("cvs -r -d $cvsroot update -A $module");
}

if ( !$user_is_locker )
{
	print "$me: Adding to watch and editors list.\n";
	system("cvs -d $cvsroot edit $module");
	system("cvs -d $cvsroot watch add -a edit $module");
	if ( !$offsite )
	{
		system("chgrp guestpc $module") unless $?;
		system("chmod g+w $module") unless $?;
	}
}

print "done\n" unless $?;
