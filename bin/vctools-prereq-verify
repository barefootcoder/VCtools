#! /usr/bin/env perl

# ##################################################################################################
# This program finds and installs all our prerequisites.  Because of chicken-and-egg issues, it has
# to run without the help of any non-core modules itself, such as Path::Class, File::Next,
# Perl6::Slurp, IO::Prompter, etc etc.  So this is a bit crude, but it should always work.
# ##################################################################################################

use strict;									# using this instead of 5.012 so we can do a better error message (below)
use autodie;								# not doing :all here so we don't die if missing IPC::System::Simple
use warnings;


# this section is reproduced from bin/vc
# for the same reasons it was copied to bin/vctools-create-config
# two differences here:
#	#	we omit the local::lib stuff, since our job is to set that up
#		(possibly for the first time ever)
#	#	we need to use $base_dir later in the script, so we declare it outside the BEGIN
my $base_dir;
BEGIN
{
	use Cwd;
	use File::Spec;
	use File::Basename;

	$base_dir = dirname dirname Cwd::realpath($0);
	my $lib_dir = File::Spec->catfile( $base_dir, 'lib' );

	unshift @INC, $lib_dir;
}

my $ME = basename $0;

use App::VC::ModuleList;


######################
# MAIN
######################

# first thing, make sure we're using a moderately modern perl
verify_perl_version();

# now make sure we can find cpanm
# (won't get far without that
verify_cpanm();

# last check: make sure extlib/ is there
verify_extlib_dir();

# now just let cpanm go to town
print "\nInstalling necessary CPAN modules LOCALLY (not messing with your system):\n\n";
install_all_modules($base_dir);


######################
# SUBS
######################

sub verify_perl_version
{
	unless (eval { require 5.012 })
	{
		print STDERR "\n$ME: $@\n";
		print STDERR "Please consider using perlbrew to get the minimum necessary version of Perl.\n\n";
		exit 1;
	}
}


sub verify_cpanm
{
	unless (`cpanm --version`)
	{
		print STDERR "\n$ME: can't locate `cpan' in your path.  Please install it.\n";
		exit 1;
	}
}


sub verify_extlib_dir
{
	unless (-d File::Spec->catfile($base_dir, 'extlib'))
	{
		print STDERR "\n$ME: can't locate my extlib/ directory.  Either you didn't get the full\n";
		print STDERR "package, or perhaps you moved me to a different directory (don't do that).\n";
		print STDERR "Either way, I can't continue.  Sorry.\n\n";
		exit 1;
	}
}
