#! /usr/bin/env perl

use strict;									# using this instead of 5.012 so we can do a better error message
use autodie;								# not doing :all here so we don't die if missing IPC::System::Simple
use warnings;


# determine proper @INC using only core modules
# once we do that, we can proceed to load other modules
BEGIN
{
	use Cwd;
	use File::Spec;
	use File::Basename;

	# turn /something/something/bin/thisscript into /something/something
	# (have to make sure we resolve symlinks and relative paths too)
	my $bin_dir = dirname Cwd::realpath($0);
	my $base_dir = dirname $bin_dir;
	my $extlib_dir = File::Spec->catfile( $base_dir, 'extlib' );
	my $lib_dir = File::Spec->catfile( $base_dir, 'lib' );
	my $perlbrew_dir = File::Spec->catfile( $extlib_dir, 'perlbrew' );

	# use perlbrewed Perl if present, and we're not already using it
	if ($ARGV[0] eq 'NORESPAWN')
	{
		shift;
	}
	elsif ( -d $perlbrew_dir )
	{
		# okay, it's present, and we're not already using it
		# just proxy to `vc-perlbrew`, which will handle all the env stuff and then proxy back to us
		my $perlbrew_run = File::Spec->catfile( $bin_dir, 'vc-perlbrew' );
		die("$0: perlbrew dir found, but no vc-perlbrew bin") unless -x $perlbrew_run;
		exec $perlbrew_run, RUNVC => @ARGV;
	}

	# set up our local::lib, if it isn't already set up
	unless ( grep { -d && Cwd::realpath($_) eq $extlib_dir } @INC )		# `$extlib_dir` already `realpath`ed from above
	{
		require local::lib;
		local::lib->import($extlib_dir);
	}

	# now jam our lib/ into @INC
	# (not using `use lib` for same reasons as above)
	unshift @INC, $lib_dir;
}

# Set up debugging if requested.
my $debug = 0;
shift and $debug = $1 if $ARGV[0] =~ /DEBUG=(\d+)/;
warn("debug set to $debug") if $debug;
print "using Perl: $^X, version $^V\n" if $debug >= 2;


# We're going to load modules very cautiously so that, if anything is missing, we can catch that and
# possibly do something about it.  This is handled by the check_prereqs() function below, which is
# mostly a moderately thin wrapper around bin/vctools-prereq-verify (which is just a separate
# program so we can move all that logic into its own file, and it's also convenient to be able to
# run it directly).  So, if just about anything goes wrong, we'll call check_prereqs().  If it
# happens _not_ to be a missing prereq, that will be handled appropriately as well.

eval { require Path::Class } ? Path::Class->import                                   : check_prereqs();
eval { require Debuggit    } ? Debuggit->import( DataPrinter => 1, DEBUG => $debug ) : check_prereqs();


# now we can go get the app class
check_prereqs() unless eval { require App::VC };

# now just run the damn thing
# (not sure this could generate a missing module error, but, hey, why take chances?)
check_prereqs() unless eval { App::VC->run; 1; };



sub check_prereqs
{
	# make sure this is something we can do something about
	die if $@ =~ m{Can't locate App/VC/};								# programmer error
	die unless $@ =~ /Can't locate .* in \@INC/ or $@ =~ /Perl .* required/;
	print STDERR "Attempting to correct: $@\n" if $debug;

	my $prereq_checker = File::Spec->catfile( dirname(Cwd::realpath($0)), 'vctools-prereq-verify' );
	unless ( -r $prereq_checker )
	{
		print STDERR "$0: cannot find the program to check that prerequisites are installed\n";
		print STDERR "I expected to find $prereq_checker,\n";
		if ( -e $prereq_checker )
		{
			print STDERR "which is either the wrong sort of file, or lacks proper permissions.\n";
		}
		else
		{
			print STDERR "which is not there.  Looks like either you didn't get the full package,\n";
			print STDERR "or perhaps you moved me to a different directory (don't do that).\n";
		}
		print STDERR "Either way, I can't continue.  Sorry.\n\n";
		exit 1;
	}

	# since we didn't do :all with autodie, we have to check this ourselves
	die("$0: some prerequisistes may not be installed [$?]") unless system($^X, $prereq_checker) == 0;

	print STDERR "\nThe problem should be corrected now.\n";
	print STDERR "Please try your command again.\n\n";
	exit 0;
}
